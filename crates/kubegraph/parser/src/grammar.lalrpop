use std::str::FromStr;

use kubegraph_api::vm::{BuiltInFunctionExpr, FunctionExpr, Literal, Number};

use crate::*;


grammar;

pub Script: Script = {
    <values: MultiLine<Stmt>> => Script(values),
};

Stmt: Stmt = {
    <lhs: Literal> "=" <rhs: Expr> => Stmt::Set {
        lhs,
        rhs,
    },
    // "if" <r#if: Expr> ":" <then: MultiLine<Stmt>> => Stmt::IfThen {
    //     r#if,
    //     then,
    //     r#else: None,
    // },
};

pub Filter: Filter = {
    <value: Literal> "!" => Filter::Ensure {
        value,
    },
    <value: OrExpr> => Filter::Expr {
        value,
    },
};

Expr: Expr = {
    <x: OrExpr> => x,
    <x: AddExpr> => x,
};

OrExpr: Expr = {
    <x: AndExpr> => x,
    <lhs: OrExpr> "or" <rhs: AndExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Or,
    },
};

AndExpr: Expr = {
    <x: CmpExpr> => x,
    <lhs: AndExpr> "and" <rhs: CmpExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::And,
    },
};

CmpExpr: Expr = {
    <lhs: CmpExprBase> "==" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Eq,
    },
    <lhs: CmpExprBase> "!=" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Ne,
    },
    <lhs: CmpExprBase> ">=" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Ge,
    },
    <lhs: CmpExprBase> ">" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Gt,
    },
    <lhs: CmpExprBase> "<=" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Le,
    },
    <lhs: CmpExprBase> "<" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Lt,
    },
};

CmpExprBase: Expr = {
    CmpExpr => <>,
    AddExpr => <>,
}

AddExpr: Expr = {
    <x: MulExpr> => x,
    <lhs: AddExpr> "+" <rhs: MulExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Add,
    },
    <lhs: AddExpr> "-" <rhs: MulExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Sub,
    },
};

MulExpr: Expr = {
    <x: UnaryExpr> => x,
    <lhs: MulExpr> "*" <rhs: UnaryExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Mul,
    },
    <lhs: MulExpr> "/" <rhs: UnaryExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Div,
    },
};

UnaryExpr: Expr = {
    <value: UnitExpr> => value,
    "+" <value: UnitExpr> => value,
    "-" <value: UnitExpr> => Expr::Unary {
        value: Box::new(value),
        op: UnaryExpr::Neg,
    },
    "!" <value: UnitExpr> => Expr::Unary {
        value: Box::new(value),
        op: UnaryExpr::Not,
    },
};

UnitExpr: Expr = {
    <value: Value> => Expr::Identity {
        value,
    },
    <op: FunctionOp> "(" <args: Comma<Expr>> ")" => Expr::Function {
        op,
        args,
    },
    "(" <x: Expr> ")" => x,
};

FunctionOp: FunctionExpr = {
    <name: BuiltInFunctionOp> => FunctionExpr::BuiltIn(name),
    <name: Literal> => FunctionExpr::Custom(name),
};

BuiltInFunctionOp: BuiltInFunctionExpr = {
    "max" => BuiltInFunctionExpr::Max,
    "min" => BuiltInFunctionExpr::Min,
};

Value: Value = {
    <x: Number> => Value::Number(x),
    <x: Literal> => Value::Variable(x),
};

pub Provide: Provide = {
    <value: Literal> => Provide::Ensure {
        value,
    },
};

Literal: Literal = {
    <s: r"[a-z]+([\._][a-z]+)*"> => Literal(s.into()),
};

Number: Number = {
    <s: r"-?[0-9]+(\.[0-9]*)?"> => Number::new(f64::from_str(s).unwrap()),
};

Comma<T>: Vec<T> = {
    => Vec::new(),
    CommaOne<T> => <>,
};

CommaOne<T>: Vec<T> = {
    <e: T> <v: ("," <T>)*> ","? => {
        let mut v = v;
        v.insert(0, e);
        v
    }
};

MultiLine<T>: Vec<T> = {
    <mut v: (<T> ";")*> <e: T?> => match e {
        Some(e) => {
            v.push(e);
            v
        },
        None => v,
    } 
};
