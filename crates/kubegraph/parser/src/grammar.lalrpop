use std::str::FromStr;

use kubegraph_api::vm::Number;

use crate::*;


grammar;

pub Script: Script = {
    <values: MultiLine<Stmt>> => Script(values),
};

Stmt: Stmt = {
    <lhs: Literal> "=" <rhs: Expr> => Stmt::Set {
        lhs,
        rhs,
    },
    // "if" <r#if: Expr> ":" <then: MultiLine<Stmt>> => Stmt::IfThen {
    //     r#if,
    //     then,
    //     r#else: None,
    // },
};

pub Filter: Filter = {
    <value: Literal> "!" => Filter::Ensure {
        value,
    },
    <value: Expr> => Filter::Expr {
        value,
    },
};

Expr: Expr = {
    <x: OrExpr> => x,
};

OrExpr: Expr = {
    <x: AndExpr> => x,
    <lhs: OrExpr> "or" <rhs: AndExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Or,
    },
};

AndExpr: Expr = {
    <x: CompExpr> => x,
    <lhs: AndExpr> "and" <rhs: CompExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::And,
    },
};

CompExpr: Expr = {
    <x: AddExpr> => x,
    <lhs: CompExpr> "==" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Eq,
    },
    <lhs: CompExpr> "!=" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Ne,
    },
    <lhs: CompExpr> ">=" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Ge,
    },
    <lhs: CompExpr> ">" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Gt,
    },
    <lhs: CompExpr> "<=" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Le,
    },
    <lhs: CompExpr> "<" <rhs: AddExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Lt,
    },
    <lhs: Literal> "->" <rhs: Literal> => Expr::Feature {
        lhs,
        rhs,
    },
};

AddExpr: Expr = {
    <x: MulExpr> => x,
    <lhs: AddExpr> "+" <rhs: MulExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Add,
    },
    <lhs: AddExpr> "-" <rhs: MulExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Sub,
    },
};

MulExpr: Expr = {
    <x: UnaryExpr> => x,
    <lhs: MulExpr> "*" <rhs: UnaryExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Mul,
    },
    <lhs: MulExpr> "/" <rhs: UnaryExpr> => Expr::Binary {
        lhs: Box::new(lhs),
        rhs: Box::new(rhs),
        op: BinaryExpr::Div,
    },
};

UnaryExpr: Expr = {
    <value: UnitExpr> => value,
    "-" <value: UnitExpr> => Expr::Unary {
        value: Box::new(value),
        op: UnaryExpr::Neg,
    },
    "!" <value: UnitExpr> => Expr::Unary {
        value: Box::new(value),
        op: UnaryExpr::Not,
    },
};

UnitExpr: Expr = {
    <value: Value> => Expr::Identity {
        value,
    },
    "(" <x: Expr> ")" => x,
};

Value: Value = {
    <x: Number> => Value::Number(x),
    <x: Literal> => Value::Variable(x),
};

pub Provide: Provide = {
    <value: Literal> => Provide::Ensure {
        value,
    },
    <lhs: Literal> "->" <rhs: Literal> => Provide::Feature {
        lhs,
        rhs,
    },
};

Literal: Literal = {
    <s: r"[a-z]+([\._][a-z]+)*"> => Literal(s.into()),
};

Number: Number = {
    <s: r"-?[0-9]+(\.[0-9]*)?"> => Number::new(f64::from_str(s).unwrap()),
};

MultiLine<T>: Vec<T> = {
    <mut v: (<T> ";")*> <e: T?> => match e {
        Some(e) => {
            v.push(e);
            v
        },
        None => v,
    } 
};
