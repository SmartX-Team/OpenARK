# Configure environment variables
export ALPINE_VERSION := env_var_or_default('ALPINE_VERSION', '3.19')
export DEBIAN_IMAGE := env_var_or_default('DEBIAN_IMAGE', 'docker.io/library/ubuntu')
export DEBIAN_IMAGE_VERSION := env_var_or_default('DEBIAN_IMAGE_VERSION', '24.04')
export NGC_CLI_API_KEY := env_var('NGC_CLI_API_KEY')
export OCI_BUILD_LOG_DIR := env_var_or_default('OCI_BUILD_LOG_DIR', './logs/')
export OCI_IMAGE := env_var_or_default('OCI_IMAGE', 'quay.io/ulagbulag/openark-vine-desktop')
export OCI_IMAGE_VERSION := env_var_or_default('OCI_IMAGE_VERSION', 'latest')
export OCI_PLATFORMS := env_var_or_default('OCI_PLATFORMS', 'linux/amd64')

default:
  @just push 'ubuntu'

build DIST *ARGS:
  mkdir -p "${OCI_BUILD_LOG_DIR}"
  docker buildx build \
    --build-arg "ALPINE_VERSION=${ALPINE_VERSION}" \
    --build-arg "DEBIAN_IMAGE=${DEBIAN_IMAGE}" \
    --build-arg "DEBIAN_IMAGE_VERSION=${DEBIAN_IMAGE_VERSION}" \
    --secret "id=NGC_CLI_API_KEY" \
    --file "./Dockerfile.{{ DIST }}" \
    --platform "${OCI_PLATFORMS}" \
    --progress 'plain' \
    --pull \
    --tag "${OCI_IMAGE}:{{ DIST }}-${OCI_IMAGE_VERSION}" \
    {{ ARGS }} \
    . 2>&1 | tee "${OCI_BUILD_LOG_DIR}/build-{{ DIST }}-$( date -u +%s ).log"

push DIST: (build DIST "--push")

push-all:
  for src_file in Dockerfile.*; do true \
    && dist="$(echo "${src_file}" | grep -Po '^Dockerfile\.\K.*')" \
    && just push "${dist}"; \
    done
