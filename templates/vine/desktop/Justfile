# Configure environment variables
export ALPINE_VERSION := env_var_or_default('ALPINE_VERSION', '3.19')
export K8S_VERSION := env_var_or_default('K8S_VERSION', '1.27.4')
export OCI_IMAGE := env_var_or_default('OCI_IMAGE', 'quay.io/ulagbulag/openark-vine-desktop')
export OCI_IMAGE_VERSION := env_var_or_default('OCI_IMAGE_VERSION', 'latest')

default:
  @just build 'rockylinux'

build DIST NVIDIA_IMAGE_NAME='cuda':
  docker build \
    --build-arg "ALPINE_VERSION=${ALPINE_VERSION}" \
    --build-arg "K8S_VERSION=${K8S_VERSION}" \
    --build-arg "NVIDIA_IMAGE_NAME={{ NVIDIA_IMAGE_NAME }}" \
    --file "./Dockerfile.{{ DIST }}" \
    --progress 'plain' \
    --tag "${OCI_IMAGE}:${OCI_IMAGE_VERSION}-{{ DIST }}" \
    .

push DIST NVIDIA_IMAGE_NAME='cuda': (build DIST NVIDIA_IMAGE_NAME)
  docker push "${OCI_IMAGE}:${OCI_IMAGE_VERSION}-{{ DIST }}"

push-all:
  for src_file in Dockerfile.*; do true \
    && dist="$(echo "${src_file}" | grep -Po '^Dockerfile\.\K.*')" \
    && just push "${dist}"; \
    done
